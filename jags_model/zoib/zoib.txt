## Zero one inflated beta regression
## alpha, gamma and mu all vary with group
## tau is shared by all groups

model{

	# likelihood for alpha
	for (i in 1:n){
		y.discrete[i] ~ dbern(alpha[i])
		logit(alpha[i]) <- a1[x[i]]
	}

	# likelihood for gamma
	for (i in 1:n.discrete){
		y.d[i] ~ dbern(mu[i])
		logit(mu[i]) <- b1[x.d[i]]
	}

	# likelihood for mu and tau
	for (i in 1:n.cont){
		y.c[i] ~ dbeta(p[i], q[i])
		p[i] <- mu2[i] * tau
		q[i] <- (1 - mu2[i]) * tau
		logit(mu2[i]) <- c1[x.c[i]]
	}
  
	# priors
	for (g in 1:n.grp){
		a1[g] ~ dnorm(0, .001)
	}
  
	for (g in 1:n.grp){
		b1[g] ~ dnorm(0, 1)
	}

	for (g in 1:n.grp){
		c1[g] ~ dnorm(0, .001)
	}
  
	t0 ~ dnorm(0, .01)
	tau <- exp(t0)
	
	# Define monitors
	for (g in 1:n.grp) {
		m.alpha[g] <- ilogit(a1[g])
		m.gamma[g] <- ilogit(b1[g])
		m.mu[g] <- ilogit(c1[g])
	}
	m.tau <- tau
  
	#data# x, y.d, y.c, y.discrete, x.d, x.c, n.discrete, n.cont, n, n.grp
	#monitor# m.alpha, m.gamma, m.mu, m.tau, pD, dic
}