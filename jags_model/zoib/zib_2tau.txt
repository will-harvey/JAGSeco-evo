## Variation of model 3 with differences
## Zero (or 1) inflated beta regression
## alpha and mu vary with group
## there are two values for tau
## one for the 1st (random) and a second shared by all other groups

model{

	# likelihood for alpha
	for (i in 1:n){
		y.discrete[i] ~ dbern(alpha[i])
		logit(alpha[i]) <- a1[x[i]]
	}

	# likelihood for mu and ta
	for (i in 1:n.cont){
		y.c[i] ~ dbeta(p[i], q[i])
		p[i] <- mu2[i] * tau[x.c[i]]
		q[i] <- (1 - mu2[i]) * tau[x.c[i]]
		logit(mu2[i]) <- c1[x.c[i]]
	}
  
	# priors
	for (g in 1:n.grp){
		a1[g] ~ dnorm(0, .001)
	}
  
	for (g in 1:n.grp){
		c1[g] ~ dnorm(0, .001)
	}
	
	# Tau varies for 1st group (random) and others (lizard)
	t0[1] ~ dnorm(0, .01)
	t0[2] ~ dnorm(0, .01)
	
    tau[1] <- exp(t0[1])
    tau[2] <- exp(t0[2])
    for (g in 3:n.grp) {
		tau[g] <- tau[2]
	}
	
	# Define monitors
	for (g in 1:n.grp) {
		m.alpha[g] <- ilogit(a1[g])
		m.mu[g] <- ilogit(c1[g])
		m.tau[g] <- tau[g]
	}
    
    #data# x, y.c, y.discrete, x.c, n.cont, n, n.grp
	#monitor# m.alpha, m.mu, m.tau, pD, dic
}